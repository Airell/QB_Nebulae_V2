;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; NebulaSound - voltage-controlled Csound in your Qu-bit Nebulae
;;
;; VOWELS
;; by Jane Freiheit (muffwiggler sebiiksbcs) 2/2014
;;
;; Available Controls:
;; -------------------
;; SPEED:   Pitch
;; PITCH:   Vowel AB Morph
;; LOOP START:  Vowel A (ah - eh - ih - oh - uh)
;; LOOP SIZE:  Vowel B (ah - eh - ih - oh - uh)
;; MIX:   Amp
;; GRAIN SIZE:  Voice Range (bass, tenor, countertenor, alto, soprano)
;;  GRAIN RATE:  Vibrato (or Creature Size)
;; GLIDE:   Glide
;;
;; Customization:
;; --------------
;;
;; - Adjust IMODE to choose between vibrato and Creature Size (formants will be
;; shifted for non-human voice effects)
;;
;; Let me know if something doesn't work, you have any suggestions,
;; or you have a video of your cat rocking to this.
;;

; Ported from Nebulae_V1 by:
; Chris Erin
; github.com/chriserin
; chris.erin@gmail.com
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; fof2 Example by Chuckk Hubbard 2007
; adapted to Qu-Bit Nebulae by Jane Freiheit (sebiiksbcs) 2014

;****************************************************************
; tables of formant values adapted from MiscFormants.html,
; converted into 5 tables containing all vocal ranges in blocks of 16
; index  0-15: soprano
; index 16-31: alto
; index 32-47: countertenor
; index 48-63: tenor
; index 64-80: bass
;
; -01: "a" sound    -02: "e" sound    -03: "i" sound    -04: "o" sound    -05: "u" sound
; p-5 through p-9: frequencies of 5 formants
; p-10 through p-14: decibel levels of 5 formants
; p-15 through p-19: bandwidths of 5 formants

gif1 ftgen 1, 0, 4096, 10, 1   ;f1 - sine wave
gif2 ftgen 2, 0, 4096, 7, 0, 4096, 1 ;f2 - linear rise shape

gif101 ftgen 101, 0, 80, -2, 800, 1150, 2900, 3900, 4950, 0.001, -6, -32, -20, -50, 80, 90, 120, 130, 140, 0, 800, 1150, 2800, 3500, 4950, 0.001, -4, -20, -36, -60, 80, 90, 120, 130, 140, 0, 660, 1120, 2750, 3000, 3350, 0.001, -6, -23, -24, -38, 80, 90, 120, 130, 140, 0, 650, 1080, 2650, 2900, 3250, 0.001, -6, -7, -8, -22, 80, 90, 120, 130, 140, 0, 600, 1040, 2250, 2450, 2750, 0.001, -7, -9, -9, -20, 60, 70, 110, 120, 130, 0

gif102 ftgen 102, 0, 80, -2, 350, 2000, 2800, 3600, 4950, 0.001, -20, -15, -40, -56, 60, 100, 120, 150, 200, 0, 400, 1600, 2700, 3300, 4950, 0.001, -24, -30, -35, -60, 60, 80, 120, 150, 200, 0, 440, 1800, 2700, 3000, 3300, 0.001, -14, -18, -20, -20, 70, 80, 100, 120, 120, 0, 400, 1700, 2600, 3200, 3580, 0.001, -14, -12, -14, -20, 70, 80, 100, 120, 120, 0, 400, 1620, 2400, 2800, 3100, 0.001, -12, -9, -12, -18, 40, 80, 100, 120, 120, 0

gif103 ftgen 103, 0, 80, -2, 270, 2140, 2950, 3900, 4950, 0.001, -12, -26, -26, -44, 60, 90, 100, 120, 120, 0, 350, 1700, 2700, 3700, 4950, 0.001, -20, -30, -36, -60, 50, 100, 120, 150, 200, 0, 270, 1850, 2900, 3350, 3590, 0.001, -24, -24, -36, -36, 40, 90, 100, 120, 120, 0, 290, 1870, 2800, 3250, 3540, 0.001, -15, -18, -20, -30, 40, 90, 100, 120, 120, 0, 250, 1750, 2600, 3050, 3340, 0.001, -30, -16, -22, -28, 60, 90, 100, 120, 120, 0

gif104 ftgen 104, 0, 80, -2, 450, 800, 2830, 3800, 4950, 0.001, -11, -22, -22, -50, 40, 80, 100, 120, 120, 0, 450, 800, 2830, 3500, 4950, 0.001, -9, -16, -28, -55, 70, 80, 100, 130, 135, 0, 430, 820, 2700, 3000, 3300, 0.001, -10, -26, -22, -34, 40, 80, 100, 120, 120, 0, 400, 800, 2600, 2800, 3000, 0.001, -10, -12, -12, -26, 70, 80, 100, 130, 135, 0, 400, 750, 2400, 2600, 2900, 0.001, -11, -21, -20, -40, 40, 80, 100, 120, 120, 0

gif105 ftgen 105, 0, 80, -2, 325, 700, 2700, 3800, 4950, 0.001, -16, -35, -40, -60, 50, 60, 170, 180, 200, 0, 325, 700, 2530, 3500, 4950, 0.001, -12, -30, -40, -64, 50, 60, 170, 180, 200, 0, 370, 630, 2750, 3000, 3400, 0.001, -20, -23, -30, -34, 40, 60, 100, 120, 120, 0, 350, 600, 2700, 2900, 3300, 0.001, -20, -17, -14, -26, 40, 60, 100, 120, 120, 0, 350, 600, 2400, 2675, 2950, 0.001, -20, -32, -28, -36, 40, 80, 100, 120, 120, 0


instr 1            ;table-lookup vocal synthesis

  iolaps init 120
  ifna init 1        ;f1 - sine wave
  ifnb init 2        ;f2 - linear rise shape
  itotdur init p3

  ;IMODE: define function of the "grain rate" knob: 1=vibrato, 2=creature size
  imode init 1

  kfund scale gkpitch, 0, 11.22  ; the whole tune-pitch-to-1V/oct shebang
  kfund pow 2, kfund
  kfund = 8.1757989156 * kfund

  kporta scale gkwindow, 0.002, .35   ; add portamento
  kfund portk kfund, kporta

  ioct    init 0
  iris    init .003
  idur    init .02
  idec    init .007
  iphs    init 0
  igliss init 0

  kvoiceshift scale gkdensity, 4.99, 10  ; voice range: [CCW > bass > tenor > countertenor > alto > soprano > CW]
  kvoiceshift = int(kvoiceshift) * 16   ; create 16-step increments

  kamp scale gkblend, 0, .2   ; amp
  kamp port kamp, 0.004

  ktabnum1  scale gkloopstart, 0, 4 ; set vowel A
  ktabnum2  scale gkloopsize, 0, 4 ; set vowel B
  kweightpoint ctrl14 1, 15, 16, 0, 1 ; set morph value between vowels A and B

  kform1 tabmorph kvoiceshift+0, kweightpoint, int(ktabnum1), ktabnum2, 101, 102, 103, 104, 105 ;read values
  kform2 tabmorph kvoiceshift+1, kweightpoint, int(ktabnum1), ktabnum2, 101, 102, 103, 104, 105
  kform3 tabmorph kvoiceshift+2, kweightpoint, int(ktabnum1), ktabnum2, 101, 102, 103, 104, 105
  kform4 tabmorph kvoiceshift+3, kweightpoint, int(ktabnum1), ktabnum2, 101, 102, 103, 104, 105
  kform5 tabmorph kvoiceshift+4, kweightpoint, int(ktabnum1), ktabnum2, 101, 102, 103, 104, 105

  kform1 port kform1, 0.01
  kform2 port kform2, 0.01
  kform3 port kform3, 0.01
  kform4 port kform4, 0.01
  kform5 port kform5, 0.01

  kamp1 tabmorph    kvoiceshift+5, kweightpoint, ktabnum1, ktabnum2, 101, 102, 103, 104, 105   ;read decibel
  kamp2 tabmorph    kvoiceshift+6, kweightpoint, ktabnum1, ktabnum2, 101, 102, 103, 104, 105
  kamp3 tabmorph    kvoiceshift+7, kweightpoint, ktabnum1, ktabnum2, 101, 102, 103, 104, 105
  kamp4 tabmorph    kvoiceshift+8, kweightpoint, ktabnum1, ktabnum2, 101, 102, 103, 104, 105
  kamp5 tabmorph    kvoiceshift+9, kweightpoint, ktabnum1, ktabnum2, 101, 102, 103, 104, 105

  kband1 tabmorph    kvoiceshift+10, kweightpoint, ktabnum1, ktabnum2, 101, 102, 103, 104, 105   ;read bandwidths
  kband2 tabmorph    kvoiceshift+11, kweightpoint, ktabnum1, ktabnum2, 101, 102, 103, 104, 105
  kband3 tabmorph    kvoiceshift+12, kweightpoint, ktabnum1, ktabnum2, 101, 102, 103, 104, 105
  kband4 tabmorph    kvoiceshift+13, kweightpoint, ktabnum1, ktabnum2, 101, 102, 103, 104, 105
  kband5 tabmorph    kvoiceshift+14, kweightpoint, ktabnum1, ktabnum2, 101, 102, 103, 104, 105

  kamp1    =    ampdb(kamp1)    ;convert db to linear multipliers
  kamp2    =    ampdb(kamp2)
  kamp3    =    ampdb(kamp3)
  kamp4    =    ampdb(kamp4)
  kamp5    =    ampdb(kamp5)

  kfund limit kfund, 0, 600

  if (imode <= 1) then
    kvibamt scale gkoverlap, 0, 0.07    ; vibrato...
    kvib lfo  kfund * kvibamt, 4.53
    kfund =  kfund+kvib
  elseif (imode >= 2) then
    kformratio ctrl7 1, 5, -0.7, 1.6   ; or creature size
    kformratio pow 2, kformratio
    kformratio port kformratio, 0.01
    kform1 = kform1 * kformratio
    kform2 = kform2 * kformratio
    kform3 = kform3 * kformratio
    kform4 = kform4 * kformratio
    kform5 = kform5 * kformratio
  endif

  ;osc to accent the fundamental
  abuz  buzz  0.08, kfund, 8, 1

  ;5 formants for each spectrum
  a1    fof2    kamp1, kfund, kform1, ioct, kband1, iris, idur, idec, iolaps, ifna, ifnb, itotdur, iphs, igliss
  a2    fof2    kamp2, kfund, kform2, ioct, kband2, iris, idur, idec, iolaps, ifna, ifnb, itotdur, iphs, igliss
  a3    fof2    kamp3, kfund, kform3, ioct, kband3, iris, idur, idec, iolaps, ifna, ifnb, itotdur, iphs, igliss
  a4    fof2    kamp4, kfund, kform4, ioct, kband4, iris, idur, idec, iolaps, ifna, ifnb, itotdur, iphs, igliss
  a5    fof2    kamp5, kfund, kform5, ioct, kband5, iris, idur, idec, iolaps, ifna, ifnb, itotdur, iphs, igliss

  a11    fof2    kamp1*.1, kfund, kform1*2, ioct, kband1, iris, idur, idec, iolaps, ifna, ifnb, itotdur, iphs, igliss
  a12    fof2    kamp2*.1, kfund, kform2*2, ioct, kband2, iris, idur, idec, iolaps, ifna, ifnb, itotdur, iphs, igliss
  a13    fof2    kamp3*.1, kfund, kform3*2, ioct, kband3, iris, idur, idec, iolaps, ifna, ifnb, itotdur, iphs, igliss
  ;a14    fof2    kamp4*.1, kfund, kform4*2, ioct, kband4, iris, idur, idec, iolaps, ifna, ifnb, itotdur, iphs, igliss
  ;a15    fof2    kamp5*.1, kfund, kform5*2, ioct, kband5, iris, idur, idec, iolaps, ifna, ifnb, itotdur, iphs, igliss

  ;sum and scale
  aout = (a1 + a2 + a3 + a4 + a5 + a11 + a12 + a13 + abuz) * kamp
  aout dcblock aout
  out aout
endin
